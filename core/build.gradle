plugins {
    id 'com.google.protobuf' version '0.8.12'
    id 'java-library'
}

description 'Trident-Java gRPC core classes'

def protobufVersion = '3.25.5'
def grpcVersion = '1.60.0'

configurations.all {
    resolutionStrategy {
        force("com.squareup.okio:okio:3.9.0")
        force("io.netty:netty-codec-http:4.1.111.Final")
        force("io.netty:netty-common:4.1.118.Final")
        force("io.netty:netty-handler:4.1.118.Final")
    }
}

dependencies {
//    implementation "com.google.guava:guava:$googleGuavaVersion"

    implementation project(':utils')
    implementation project(':abi')

    implementation "io.grpc:grpc-netty-shaded:$grpcVersion"
    implementation "io.grpc:grpc-netty:$grpcVersion"
    implementation "io.grpc:grpc-okhttp:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

    implementation "com.google.protobuf:protobuf-java-util:$protobufVersion"
    implementation "com.google.protobuf:protobuf-java:$protobufVersion"
    implementation "org.bouncycastle:bcprov-jdk18on:$bouncycastleVersion"

    //json parsing
    implementation "com.alibaba.fastjson2:fastjson2:2.0.55"

    implementation "io.quarkus.http:quarkus-http-core:5.3.3" //high

    implementation "org.apache.ozone:ozone:1.4.0" //high

    implementation "dnsjava:dnsjava:3.5.0" //high

    implementation "io.smallrye:smallrye-fault-tolerance-core:6.3.0" //high
//  implementation "org.apache.james.protocols:protocols-imap:3.8.0"
    implementation "com.liferay.portal:release.portal.bom:7.4.3.82" //mid
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { outputSubDir = "java" }
            }
        }
        all()*.plugins {
            grpc {
                outputSubDir = "java"
                option 'generate_streaming=true'
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src"
}

tasks.named('processResources') {
    dependsOn 'generateProto'
    dependsOn 'extractProto'
}

tasks.named('processTestResources') {
    dependsOn 'generateTestProto'
    dependsOn 'extractTestProto'
}

clean.doFirst {
    delete "src/main/java/org/tron/trident/api"
    delete "src/main/java/org/tron/trident/proto"
}

